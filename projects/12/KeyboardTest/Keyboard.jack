// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int keyboard;

    /** Initializes the keyboard. */
    function void init() {
        let keyboard = 0;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {

        let keyboard = Memory.peek(24576);
        if ( keyboard > -1 ) {
            return keyboard;
        }
        else {
            return 0;
        }
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c, key;

        let c = 0;
        let key = 0;
        // print black square
        do Output.printChar(0);

	while (key = 0) {
	    let key = Keyboard.keyPressed();
	}
	let c = key;
	while (~(key = 0)) {
	    let key = Keyboard.keyPressed();
	}

        do Output.backSpace();
        do Output.printChar(c);
        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String str;
        var char c;
        var boolean ok;
    
        let ok = false;
        let c = 0;
        let str = String.new(100);

        do Output.printString(message);

	while ( ~ok ) {
	    let c = Keyboard.readChar();
            if ( (c < 128) & (c > 31) ) {
                let str = str.appendChar(c);
            }
            if ( c = 129 ) { 
                do str.eraseLastChar();
		//do Output.backSpace();
            }
            if ( c = 128 ) { let ok = true;}
	}

        return str;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String str;
        var char c;
        var boolean ok;
    
        let ok = false;
        let c = 0;
        let str = String.new(100);

        do Output.printString(message);

	while ( ~ok ) {
	    let c = Keyboard.readChar();

            if ( (c < 128 ) & (c > 31 )  ) {
                let str = str.appendChar(c);
            }

            if ( c = 129 ) { 
                do str.eraseLastChar();
		//do Output.backSpace();
            }
            if ( c = 128 ) { let ok = true;}
	}

        return str.intValue();
        
    }
}
